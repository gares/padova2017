<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml">
  <head>
    <meta http-equiv="content-type" content="text/html;charset=utf-8" />

    <link rel="stylesheet" href="jscoq/ui-external/bootstrap.min.css" />

<title>lesson2</title><script type='text/javascript' src='https://cdnjs.cloudflare.com/ajax/libs/mathjax/2.7.1/MathJax.js?config=TeX-MML-AM_CHTML'> </script> <script type='text/javascript'> MathJax.Hub.Config({ 'HTML-CSS': { preferredFont: 'STIX' }, tex2jax: { inlineMath: [['XXX','XXX']] } }); </script>
<link rel="stylesheet" href="local.css" />
  </head>

<body>
  <div id="ide-wrapper" class="toggled">
  <div id="code-wrapper">
  <div id="document">

<div><textarea id='coq-ta-1'>
</textarea></div>
<div><p>
Note sulla lezione 1
<p>
<ul class="doclist">
  <li> <tt>match .. with .. end</tt> è un termine e può
  essere usato dovunque, non solo alla radice

  </li>
</ul>
</div>
<div><textarea id='coq-ta-2'>

Fixpoint odd  n :=
  match n with
  | O => false
  | S p =>
     match p with
     | O => true
     | S q => odd q
     end
  end.

Definition notb b := match b with true => false | false => true end.

Fixpoint odd2  n :=
  match n with
  | O => false
  | S p => notb (odd2 p)
  end.
</textarea></div>
<div><p>
<p>
  E le chiamae ricorsive sono accettate anche su
  sotto-sotto-termini
<p>
<ul class="doclist">
  <li> I tipi induttivi ammetteno un principio di induzione/
  ricorsione standard

  </li>
</ul>
</div>
<div><textarea id='coq-ta-3'>

Check nat_rect.
Check (nat_rect (fun x => bool)).

Definition odd3 :=
  nat_rect (fun x => bool)
    false
    (fun p b => notb b).
Eval compute in odd3 4.
Eval compute -[notb] in odd3.

</textarea></div>
<div><p>
Tipi di dato indiciati e i
    casi impossibili </div>
<div><textarea id='coq-ta-4'>
Module Vector.

  Inductive vect (A : Type) : nat -> Type :=
  | vnil : vect A 0
  | vcons : forall n, A -> vect A n -> vect A (1+n).

  Arguments vnil {_}.
  Arguments vcons {_ _} _ _.

  Definition hd {A n} (v : vect A (S n)) : A :=
  match v in vect _ l return
    match l with
    | S _ => A
    | O => bool
    end
  with
  | vcons x xs => x
  | vnil => true
  end.

  Fail Check hd (vnil nat).
  Eval compute in
    hd (vcons 3 (vcons 2 vnil)).
  Check  (vcons 3 (vcons 2 vnil)).

End Vector.

</textarea></div>
<div><p>
<hr/>
 <p>
 <div class='slide vfill'>  
<h2>
 Lezione 2
</h2>

<p>
<ul class="doclist">
  <li> Curry Howard: la logica di Coq e le prime dimostrazioni
<ul class="doclist">
    <li> Programma : Tipo = Dimostrazione : Enunciato

    </li>
  <li> connettivi

  </li>
  <li> vero e falso

  </li>
  <li> prove di logica proposizionale

  </li>
  <li> uguaglianza

  </li>
  <li> prove equazionali

  </li>
  <li> quantificatori

  </li>
  <li> prove in logica del primo ordine
<p>
  
  </li>
  </ul>
  
  </li>
<li> Esercizi

</li>
</ul>
<p>
</div> </div>
<div><textarea id='coq-ta-5'>

From mathcomp Require Import ssreflect.

</textarea></div>
<div><p>
<hr/>
 <p>
 <div class='slide'> 
<h2>
 Fare una dimostrazione è come abitare un tipo
</h2>
</div>
<div><textarea id='coq-ta-6'>

Definition prova1 : nat := O.
Check prova1.

</textarea></div>
<div><p>
Però lo facciamo in modo interattivo, usando
    dei comandi (tattiche) che costruiscono termini
    per noi. </div>
<div><textarea id='coq-ta-7'>
Lemma prova2 : nat.
Proof.
apply: O.
Qed.

Print prova2.

</textarea></div>
<div><p>
I tipi (di dato) che abbiamo visto nella prima
    lezione non sono adatti a esprimere proprietà
    o enunciare veri e propri teoremi.
<p>
    Ne definiamo quindi altri... 
<p>
 <div class="concepts">
Concetti:
<p>
<ul class="doclist">
  <li> dimostrare = costruire termini passo passo

  </li>
<li> <tt>Lemma nome : tipo. Proof. ...prova... Qed.</tt>

</li>
<li> <tt>apply: t</tt> usa <tt>t</tt> per dimostrare il goal corrente, ovvero
  <tt>t</tt> è un termine che abita il tipo del goal
 </div>

</li>
</ul>
</div>
<p>
<p>
<hr/>
 <p>
 <div class='slide'> 
<h2>
 Connettivi
</h2>
<p>
 $$P \to Q$$ </div>
<div><textarea id='coq-ta-8'>

Lemma example1 P : P -> P.
Proof.
apply: (fun (p : P) => p).
Qed.

</textarea></div>
<div><p>
dimostrare una implicazione (introduzione) </div>
<div><textarea id='coq-ta-9'>
Lemma example2 P : P -> P.
Proof.
move=> p.
apply: p.
Qed.

</textarea></div>
<div><p>
Usare una implicazione (MP) </div>
<div><textarea id='coq-ta-10'>
Lemma example3 P Q : Q -> (Q -> P) -> P.
Proof.
apply: (fun (q : Q) => fun (f : Q -> P) => f q).
Qed.

Lemma example4 P Q : Q -> (Q -> P) -> P.
Proof.
move=> q f.
apply: (f _).
apply: q.
Qed.

</textarea></div>
<div><p>
$$P \land Q$$ </div>
<div><textarea id='coq-ta-11'>

Inductive and P Q := conj (p : P) (q : Q).

Infix "/\ " := and : type_scope.
Arguments conj {P Q} p q.

Lemma andC P Q : P /\ Q -> Q /\ P.
Proof.
move=> pq.
case: pq.
move=> p q.
apply: (conj _ _).
  apply: q.
apply: p.
Qed.

</textarea></div>
<div><p>
Ricordate l'esercizio 8bis? </div>
<div><textarea id='coq-ta-12'>
Lemma andC2 P Q : P /\ Q -> Q /\ P.
Proof.
apply: (fun x => match x with conj p q => conj q p end).
Qed.

</textarea></div>
<div><p>
$$P \lor Q$$ </div>
<div><textarea id='coq-ta-13'>

Inductive or P Q :=
  | left (p : P)
  | right (q : Q).

Arguments left {P Q} p.
Arguments right {P Q} q.
Infix "\/" := or  : type_scope.

Lemma orC P Q : P \/ Q -> Q \/ P.
Proof.
move=> pq.
case: pq.
  move=> p.
  apply: right.
  apply: p.
move=> q.
apply: left.
apply: q.
Qed.

Lemma orC2 P Q : P \/ Q -> Q \/ P.
Proof.
apply: (fun pq =>
          match pq with
          | left p => right p
          | right q => left q
          end).
Qed.

</textarea></div>
<div><p>
<p>
 <div class="concepts">
 Concetti:
<p>
<ul class="doclist">
  <li> congiunzione <tt>/\</tt>

  </li>
<li> disgiunzione <tt>\/</tt>

</li>
<li> <tt>move=> nome</tt>

</li>
<li> <tt>case: termine</tt>

</li>
</ul>
 </div>
<p>
 </div> <p>
<hr/>
 <p>
 <div class='slide'> 
<h2>
 Vero e falso
</h2>
<p>
 $$\top$$ </div>
<div><textarea id='coq-ta-14'>

Inductive True := I.

Lemma exampleI : True -> True.
Proof.
move=> i.
case: i.
apply: I.
Qed.

</textarea></div>
<div><p>
$$\bot$$ </div>
<div><textarea id='coq-ta-15'>

Inductive False := .

Lemma ex_falso P : False -> P.
Proof.
move=> abs.
case: abs.
Qed.

Lemma ex_falso2 P : False -> P.
Proof.
apply: (fun (abs : False) => match abs with end).
Qed.

</textarea></div>
<div><p>
$$\neg$$ </div>
<div><textarea id='coq-ta-16'>

Definition not P := P -> False.

Notation "~ p" := (not p) : type_scope.

Lemma contradiction P : P -> not P -> False.
Proof.
move=> p np.
unfold not in np.
apply: (np _).
apply: p.
Qed.

</textarea></div>
<div><p>
Note: questo lemma è uguale all'esempio 4 <p>
<p>
 <div class="concepts">
 Concetti:
<p>
<ul class="doclist">
  <li> vero e falso: <tt>True</tt>, <tt>False</tt>

  </li>
<li> negazione: <tt>~</tt>

</li>
<li> sostituire il nome con il corpo: <tt>unfold name in hyp</tt>
   (passo non strettamente necessario)

</li>
</ul>
 </div>
<p>
</div> <p>
<hr/>
 <p>
 <div class='slide'> 
<h2>
 Uguaglianza
</h2>

<p>
  Ora ci siamo stufati della logica proposizionale
  e passiamo a quella del primo ordine.
<p>
  Ci serve un predicato, e partiamo dal più difficle ;-)
<p>
  Il tipo <tt>Id</tt> (che in Coq si chiama <tt>eq</tt>)
<p>
</div>
<div><textarea id='coq-ta-17'>

Inductive eq (A : Type) (a : A) : A -> Type :=
  erefl : eq A a a.

Check eq.
Check eq nat 3 4.
Check eq bool true true.
</textarea></div>
<div><p>
<tt>forall</tt> lega un termine </div>
<div><textarea id='coq-ta-18'>
Check erefl.

Arguments eq {A} a _.
Arguments erefl {A} a. (* nota: solo 2 argomenti *)

Infix "=" := eq : type_scope.

Check (3 = 4).

(* Usiamo e dimostriamo un'uguaglianza *)

Lemma eq_sym_nat (x y : nat) : x = y -> y = x.
Proof.
move=> E.
rewrite E.
apply: (erefl y).
Qed.

Lemma eq_sym_nat2 (x y : nat) : x = y -> y = x.
Proof.
apply: (fun E : x = y =>
   match E (*in (_ = a) return a = x*) with
   | erefl => erefl x
   end).
Qed.

</textarea></div>
<div><p>
eq (Id) descrive una uguaglianza computazionale </div>
<div><textarea id='coq-ta-19'>

Lemma test_computation : 3 + 2 = 5.
Proof.
apply: erefl.
Qed.

Eval compute in 3 + 2.

</textarea></div>
<div><p>
 <div class="concepts">
 Concetti:
<p>
<ul class="doclist">
  <li> uguaglianza <tt>=</tt> (Id nelle note del corso)

  </li>
<li> Usare un'uguaglianza: <tt>rewrite</tt>

</li>
<li> <tt>erefl</tt> dimostra <tt>eq</tt> per termini uguali una
   volta nomalizzati

</li>
<li> tipare un <tt>match</tt> (la clausola <tt>return</tt> e
   il tipo <quote>da fuori</quote> e <quote>da dentro</quote>

</li>
</ul>
 </div>
<p>
</div> <p>
<hr/>
 <p>
 <div class='slide'> 
<h2>
 Quantificatori
</h2>

<p>
   Piccolo ripasso:
<p>
   Il <tt>forall</tt> è primitivo, è stato usato
<p>
<ul class="doclist">
  <li> per legare tipi <tt>forall A : Type, list A -> nat</tt>

  </li>
<li> per legare termini <tt>forall x : A, eq A x x</tt>

</li>
<li> per legare.. niente <tt>A -> B = forall dummy : A, B</tt>

</li>
</ul>
   Il dominio di <tt>forall</tt>
<p>
<ul class="doclist">
  <li> il tipo dei tipi <tt>forall A : Type, ..</tt>

  </li>
<li> un tipo di dato <tt>forall x : nat, ...</tt>

</li>
<li> il tipo delle funzioni <tt>forall f : nat -> nat, ...</tt>

</li>
</ul>
<p>
    Ora usiamo $$\forall x:A, P x$$
    che può essere dimostrato fornendo un
    funzione che data una prova di <tt>A</tt> (chiamata <tt>x</tt>)
    produce una prova di <tt>P x</tt> </div>
<div><textarea id='coq-ta-20'>

Lemma test_forall : forall x : nat, x = x.
Proof.
apply: (fun x : nat => erefl x).
Qed.

Lemma test_forall2 : forall x : nat, x = x.
Proof.
move=> x.
apply: erefl x.
Qed.

</textarea></div>
<div><p>
Nota: del tutto simile alla prova di <tt>P -> P</tt> fatta
nella slide 2 </div>
<div><textarea id='coq-ta-21'>

Lemma test_uso_forall :
	 (forall x : nat, x * 0 = 0) -> 3 * 0 = 0.
Proof.
move=> mulx0.
Check (mulx0 3).
apply: (mulx0 3).
Qed.

</textarea></div>
<div><p>
Nota: anche in questo caso il comportamento di XXX\forallXXX
è molto simile a quello di XXX\toXXX <p>
<p>
    Ora definiamo $$\exists x:A, P x$$
    che può essere dimostrato fornendo un
    testimone <tt>t : A</tt> e una prova <tt>p : P t</tt> </div>
<div><textarea id='coq-ta-22'>

Inductive sig (A : Type) (P : A -> Type) :=
  sigI (a : A) (p : P a).

</textarea></div>
<div><p>
Nota: è molto simile alla coppia, ma il tipo
    della seconda componente dipende val valore della
    prima </div>
<div><textarea id='coq-ta-23'>

Check sigI.
</textarea></div>
<div><p>
<tt>forall</tt> lega un predicato/funzione </div>
<div><textarea id='coq-ta-24'>
Check sig.

Check (fun x : nat => x = 3).

Arguments sig {_} _.
Arguments sigI {_ _} _ _.

Check sig (fun x => x = 3).

Notation "'exists' x : A , p" := (sig (fun x : A => p)) (at level 200, x ident).

Definition divides d n := exists q : nat, d * q = n.

Lemma div3_15 : divides 3 15.
Proof.
exists 5.
apply:erefl.
Qed.

Lemma mulnA a b c : a * (b * c) = a * b * c.
Proof.
Admitted.

Lemma div_trans a b c : divides a b -> divides b c -> divides a c.
Proof.
move=> dab dbc.
case: dab. move=> q Eb.
case: dbc. move=> p Ec.
exists (q * p).
rewrite -Ec -Eb.
rewrite mulnA.
apply: erefl.
Qed.

</textarea></div>
<div><p>
</div> <p>
<hr/>
 <p>
 <div class='slide'> 
<h2>
 Dimostrazioni per casi
</h2>

<p>
</div>
<div><textarea id='coq-ta-25'>

Lemma andbC a b : andb a b = andb b a.
Proof.
case: a.
  case: b.
    apply: erefl.
    apply: erefl.
case: b.
  apply: erefl.
apply: erefl.
(* ripetitiva no? rifacciamola con il ; *)
Qed.

</textarea></div>
<div><p>
 <div class="concepts">
 Concetti:
<p>
<ul class="doclist">
  <li> <tt>case:</tt> funziona anche su termini che rappresentano
   dati, come <tt>bool</tt> e <tt>nat</tt>

  </li>
<li> <tt>;</tt> per combinare 2 comandi di prova

</li>
</ul>
 </div>
<p>
</div> <p>
<hr/>
 <p>
 <div class='slide'> 
<h2>
 Discriminazione dei costruttori e sotto tipi
</h2>

<p>
<ul class="doclist">
  <li> definiamo il tipo dei numeri naturali positivi

  </li>
<li> usiamolo per programmare la funzione <tt>pred</tt>

</li>
</ul>
<p>
</div>
<div><textarea id='coq-ta-26'>

Definition positivo := exists n : nat, ~ n=0.

Definition pred (x : positivo) : nat.
Proof.
case: x.
move=> n.
case: n.
  move=> abs.
  apply: (ex_falso nat).
  apply: abs.
  apply: erefl.
move=> p _.
apply: p.
Defined.

</textarea></div>
<div><p>
Dobbiamo dimostrare che, per sempio, <tt>~ 3 = 0</tt>,
    per applciare <tt>pred</tt> a <quote>3</quote>
</div>
<div><textarea id='coq-ta-27'>

Lemma not_3_0 : ~ (3 = 0).
Proof.
move=> E.
change (match 3 with O => True | S _ => False end).
rewrite E.
apply: I.
Qed.

Definition n3p : positivo := sigI 3 not_3_0.

Eval compute -[not] in pred n3p.

</textarea></div>
<div><p>
 <div class="concepts">
 Concetti:
<p>
<ul class="doclist">
  <li> <tt>sig</tt> rappresenta un sotto tipo

  </li>
<li> <tt>sigI</tt> è come <tt>pair</tt>, raggruppa due termini
   e possiamo usarlo per scrivere programmi

</li>
<li> <tt>change tipo</tt> per cambiare la forma del goal
   corrente con una convertibile

</li>
</ul>
 </div>
<p>
</div> <p>
<hr/>
 <p>
 <div class='slide'> 
<h2>
 Prove all'avanti
</h2>

<p>
</div>
<div><textarea id='coq-ta-28'>

Lemma fwd P Q :
  (P -> Q) -> P -> ~Q -> 0 = 1.
Proof.
move=> p2q p nq.
have q : Q.
  apply: p2q. apply: p.
have abs : False.
  apply: nq. apply q.
case: abs.
Qed.

</textarea></div>
<div><p>
<div class="concepts">
 Concetti:
<p>
<ul class="doclist">
  <li> <tt>have nome : tipo</tt> inizia una sottoprova

  </li>
</ul>
 </div>
<p>
</div> <p>
<hr/>
 <p>
 <div class="slide">
<p>
<h2>
 Dimostriamo (hem programmiamo) il principio di induzione per <tt>nat</tt>
</h2>

<p>
  Se le prove sono programmi, allora abitiamo questo tipo, senza troppo
  riflettere a cosa significhi.
</div>
<div><textarea id='coq-ta-29'>

Fixpoint rec (P : nat -> Type) (p0 : P 0) (pS : forall n, P n -> P (S n)) (n : nat) : P n :=
  match n with
  | O => p0
  | S q => pS q (rec P p0 pS q)
  end.

Check rec.

</textarea></div>
<div><p>
Abbiamo ottenuto il principio di induzione! 
<p>
    Beh, anche Coq lo sa fare... Infatti non appena uno
    definisce i tipo <tt>nat</tt> Coq genera questo programma
    in modo automatico:
</div>
<div><textarea id='coq-ta-30'>

Check nat_rect.

</textarea></div>
<div><p>
<div class="concepts">
 Concetti:
<p>
<ul class="doclist">
  <li> I principi di induzione non sono altro che funzioni
   ricorsive

  </li>
</ul>
 </div>
<p>
</div> <p>
<hr/>
 <p>
 <div class="slide">
<p>
<h2>
 Relazioni induttive (e non)
</h2>

<p>
  Proviamo ora ad esprimere il predicatoXXX\leqXXX
  per i numeri naturali.
<p>
  Abbiamo dues possibilità
</div>
<div><textarea id='coq-ta-31'>

Inductive is_leq (n : nat) : nat -> Type :=
  | leqO : is_leq n n
  | leqS : forall m, is_leq n m -> is_leq n (S m).

Lemma test_is_leq : is_leq 4 6.
Proof.
apply: leqS.
apply: leqS.
apply: leqO.
Qed.

Fixpoint leq (n m : nat) : bool :=
  match n with
  | O => true
  | S a => match m with
           | O => false
           | S b => leq a b
           end
  end.

Lemma test_leq : leq 4 6 = true.
Proof.
apply: erefl.
Qed.

</textarea></div>
<div><p>
</div> <hr/>
  <div class="slide"> <p>
<h3>
 Link utili:
</h3>

<p>
<ul class="doclist">
  <li> <a href="https://hal.inria.fr/inria-00258384">manuale</a> del linguaggio di prova

  </li>
<li> <a href="http://www-sop.inria.fr/marelle/math-comp-tut-16/MathCompWS/basic-cheatsheet.pdf">cheat-sheet</a>

</li>
</ul>
<p>
 </div> <p>
 <div class='slide'>
<h2>
 Esercizi
</h2>

<p>
<p>
<hr/>
 <h2>
 Abitare i seguenti tipi (in un colpo solo o con dei comandi di prova)
</h2>
</div>
<div><textarea id='coq-ta-32'>

Lemma scrambled A B C : (A /\ B) /\ C -> (C /\ A) /\ B.
Proof.
(* completa... *)
(* completa... *)
(* completa... *)
(* completa... *)
Qed.

</textarea></div>
<div><p>
<p>

<button onclick="hide('sol1')">Soluzione</button>
<div id='sol1' class='solution'>
<pre>
apply: (fun x =>
match x with
| conj (conj a b) c => conj (conj c a) b
end).
</pre>
</div>

</div>
<div><textarea id='coq-ta-33'>

Lemma eggs P Q R : (P \/ Q -> R) -> Q -> R.
Proof.
(* completa... *)
Qed.

</textarea></div>
<div><p>
<p>

<button onclick="hide('sol2')">Soluzione</button>
<div id='sol2' class='solution'>
<pre>
apply: (fun f q => f (right q)).
</pre>
</div>

</div>
<div><textarea id='coq-ta-34'>

Lemma DM P Q : (~ P /\ ~ Q) -> ~ (P \/ Q).
Proof.
(* completa... *)
(* completa... *)
Qed.

</textarea></div>
<div><p>
<p>

<button onclick="hide('sol3')">Soluzione</button>
<div id='sol3' class='solution'>
<pre>
move=> npnq. case: npnq => np nq.
move=> poq. case: poq. apply: np. apply: nq.
</pre>
</div>

<p>
 <a href="https://en.m.wikipedia.org/wiki/Peirce's_law">La formula di Peirce</a> 
<p>
<ul class="doclist">
  <li> Suggerimento: scegliere per <tt>Q</tt> una formula falsa!

  </li>
</ul>
</div>
<div><textarea id='coq-ta-35'>

Lemma Peirce :
  (forall Q P, (((P -> Q) -> P) -> P)) ->
  (forall P, P \/ ~ P).
Proof.
(* completa... *)
(* completa... *)
(* completa... *)
(* completa... *)
(* completa... *)
(* completa... *)
(* completa... *)
(* completa... *)
Qed.

</textarea></div>
<div><p>
<p>

<button onclick="hide('sol4')">Soluzione</button>
<div id='sol4' class='solution'>
<pre>
move=> cc P.
apply: (cc False).
move=> truth.
apply: right.
move=> p.
apply: truth.
apply: left.
apply: p.
</pre>
</div>

<p>
<hr/>
 <h2>
 Dimostrare che <tt>orb</tt> è commutativo
</h2>
</div>
<div><textarea id='coq-ta-36'>

Definition orb b1 b2 :=
  match b1 with
  | true => true
  | false => b2
  end.

Lemma orbC b1 b2 : orb b1 b2 = orb b2 b1.
Proof.
(* completa... *)
Qed.

</textarea></div>
<div><p>
<p>

<button onclick="hide('sol5')">Soluzione</button>
<div id='sol5' class='solution'>
<pre>
case: b1; case: b2; apply: erefl.
</pre>
</div>

<p>
<hr/>
 <h2>
 Dimostrare che <tt>orb</tt> è associativo
</h2>
</div>
<div><textarea id='coq-ta-37'>

Lemma orbA b1 b2 b3 : orb b1 (orb b2 b3) = orb (orb b1 b2) b3.
Proof.
(* completa... *)
Qed.

</textarea></div>
<div><p>
<p>

<button onclick="hide('sol6')">Soluzione</button>
<div id='sol6' class='solution'>
<pre>
case: b1; case: b2; case: b3; apply: erefl.
</pre>
</div>

<p>
<hr/>
 <h2>
 Dimostrare il seguente lemma
</h2>
</div>
<div><textarea id='coq-ta-38'>

Lemma EMb b : orb b (negb b) = true.
Proof.
(* completa... *)
Qed.

</textarea></div>
<div><p>
<p>

<button onclick="hide('sol7')">Soluzione</button>
<div id='sol7' class='solution'>
<pre>
case: b; apply: erefl.
</pre>
</div>

<p>
<hr/>
 <h2>
 Dimostra la seguente formula
</h2>

<p>
XXX
  m ^ 2 - n ^ 2 =  (m - n) * (m + n)
XXX
<p>
   Usare solo la riscrittura <tt>rewrite</tt>
   e i seguenti simboli e lemmi che sono forniti
<p>
</div>
<div><textarea id='coq-ta-39'>

Variable subn : nat -> nat -> nat.
Infix "-" := subn.
Variable expn : nat -> nat -> nat.
Infix "^" := expn.
Lemma mulnBl (x y z : nat) : (x - y) * z = x * z - y * z. Admitted.
Lemma mulnDr (x y z : nat) : x * (y + z) = x * y + x * z. Admitted.
Lemma addnC (x y : nat) : x + y = y + x. Admitted.
Lemma mulnC (x y : nat) : x * y = y * x. Admitted.
Lemma subnDl (p m n : nat) : p + m - (p + n) = m - n. Admitted.
Lemma mulnn n : n * n = n ^ 2. Admitted.

</textarea></div>
<div><p>
Traccia della prova
<pre>
(m - n) * (m + n) =
          .. = m * (m + n) - n * (m + n)
          .. = m * m + m * n - n * (m + n)
          .. = m * m + m * n - (n * m + n * n)
          .. = m * n + m * m - (n * m + n * n)
          .. = n * m + m * m - (n * m + n * n)
          .. = m * m - n * n
          .. = m ^ 2 - n * n
          .. = m ^ 2 - n ^ 2
</pre>
 </div>
<div><textarea id='coq-ta-40'>

Lemma subn_sqr m n : (m - n) * (m + n) = m ^ 2 - n ^ 2.
Proof.
(* completa... *)
(* completa... *)
(* completa... *)
(* completa... *)
(* completa... *)
(* completa... *)
(* completa... *)
(* completa... *)
(* completa... *)
Qed.

</textarea></div>
<div><p>
<p>

<button onclick="hide('sol8')">Soluzione</button>
<div id='sol8' class='solution'>
<pre>
rewrite mulnBl.
rewrite mulnDr.
rewrite mulnDr.
rewrite addnC.
rewrite mulnC.
rewrite subnDl.
rewrite mulnn.
rewrite mulnn.
apply: erefl.
</pre>
</div>

<p>
 </div> <p>
<hr/>
 <p>
<p>

<script>
alignWithTop = true;
current = 0;
slides = [];
function select_current() {
  for (var i = 0; i < slides.length; i++) {
    var s = document.getElementById('slideno' + i);
    if (i == current) {
      s.setAttribute('class','slideno selected');
    } else {
      s.setAttribute('class','slideno');
    }
  }	
}
function init_slides() {
  var toolbar = document.getElementById('panel-wrapper');
  if (toolbar) {
  var tools = document.createElement("div");
  var tprev = document.createElement("div");
  var tnext = document.createElement("div");
  tools.setAttribute('id','tools');
  tprev.setAttribute('id','prev');
  tprev.setAttribute('onclick','prev_slide();');
  tnext.setAttribute('id','next');
  tnext.setAttribute('onclick','next_slide();');
  toolbar.appendChild(tools);
  tools.appendChild(tprev);
  tools.appendChild(tnext);
  
  slides = document.getElementsByClassName('slide');
  for (var i = 0; i < slides.length; i++) {
    var s = document.createElement("div");
    s.setAttribute('id','slideno' + i);
    s.setAttribute('class','slideno');
    s.setAttribute('onclick','goto_slide('+ i +');');
    s.innerHTML = i;
    tools.appendChild(s);
  }
  select_current();
  } else {
  //retry later
  setTimeout(init_slides,100);	  
  }
}
function on_screen(rect) {
  return (
    rect.top >= 0 &&
    rect.top <= (window.innerHeight || document.documentElement.clientHeight)
  );
}
function update_scrolled(){
  for (var i = slides.length-1; i >= 0; i--) {
    var rect = slides[i].getBoundingClientRect();
      if (on_screen(rect)) {
        current = i;
        select_current();	
    }
  }
}
function goto_slide(n) {
  current = n;
  var element = slides[current];
  console.log(element);
  element.scrollIntoView(alignWithTop);
  select_current();
}
function next_slide() {
  current++;
  if (current >= slides.length) { current = slides.length - 1; }
  var element = slides[current];
  console.log(element);
  element.scrollIntoView(alignWithTop);
  select_current();
}
function prev_slide() {
  current--;
  if (current < 0) { current = 0; }
  var element = slides[current];
  element.scrollIntoView(alignWithTop);
  select_current();
}
function hide (element_id) {
  element = document.getElementById(element_id);
    if (element.style.display != 'block') {
      element.style.display = 'block';
    } else {
      element.style.display = 'none';
    }
}
window.onload = init_slides;
window.onscroll = update_scrolled;
</script>
 </div>
<div><textarea id='coq-ta-41'>
</textarea></div>
<script type="text/javascript">
 var coqdoc_ids = ['coq-ta-1',  'coq-ta-2',  'coq-ta-3',  'coq-ta-4', 
                  'coq-ta-5',  'coq-ta-6',  'coq-ta-7',  'coq-ta-8', 
                  'coq-ta-9',  'coq-ta-10',  'coq-ta-11',  'coq-ta-12', 
                  'coq-ta-13',  'coq-ta-14',  'coq-ta-15',  'coq-ta-16', 
                  'coq-ta-17',  'coq-ta-18',  'coq-ta-19',  'coq-ta-20', 
                  'coq-ta-21',  'coq-ta-22',  'coq-ta-23',  'coq-ta-24', 
                  'coq-ta-25',  'coq-ta-26',  'coq-ta-27',  'coq-ta-28', 
                  'coq-ta-29',  'coq-ta-30',  'coq-ta-31',  'coq-ta-32', 
                  'coq-ta-33',  'coq-ta-34',  'coq-ta-35',  'coq-ta-36', 
                  'coq-ta-37',  'coq-ta-38',  'coq-ta-39',  'coq-ta-40', 
                  'coq-ta-41'];
</script>
  </div> <!-- /#document     -->
  </div> <!-- /#code-wrapper -->
  </div> <!-- /#ide-wrapper  -->

  <script src="./jscoq/ui-js/jscoq-loader.js" type="text/javascript"></script>
  <script type="text/javascript">
    var coq;

    loadJsCoq('./jscoq/')
              .then(loadJs("./jscoq/ui-external/CodeMirror/addon/runmode/runmode"))
              .then(loadJs("./jscoq/ui-external/CodeMirror/addon/runmode/colorize"))
              .then( function () {
                  var coqInline = document.getElementsByClassName("inline-coq");
                  CodeMirror.colorize(coqInline); })
              .then( function () {
                  coq = new CoqManager (coqdoc_ids,
                                       { base_path: './jscoq/', init_pkgs: ['init'] }
                  );
              });
  </script>
</body>
</html>

